find_package(Threads REQUIRED)
include(ExternalProject)

set(GTEST_LOCATION "${CMAKE_SOURCE_DIR}/vendor/gtest")

set(GOOGLE_TEST_CMAKE_ARGS
  -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
  -DCMAKE_BUILD_TYPE=Release
  -DCMAKE_INSTALL_PREFIX=${GTEST_LOCATION}
  -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES}
)

set(GTEST_PREFIX "${CMAKE_BINARY_DIR}/vendor_srcs/googleTest")

ExternalProject_Add(googleTestExternal
  PREFIX "${CMAKE_SOURCE_DIR}/vendor/build"

  GIT_REPOSITORY https://github.com/google/googletest.git
  GIT_TAG release-1.8.0

  CMAKE_ARGS ${GOOGLE_TEST_CMAKE_ARGS}
)

set(LIBPREFIX "${CMAKE_STATIC_LIBRARY_PREFIX}")
set(LIBSUFFIX "${CMAKE_STATIC_LIBRARY_SUFFIX}")
set(GTEST_INCLUDES "${GTEST_LOCATION}/include")
set(GTEST_LIBRARY "${GTEST_LOCATION}/lib/${LIBPREFIX}gtest${LIBSUFFIX}")
set(GTEST_MAINLIB "${GTEST_LOCATION}/lib/${LIBPREFIX}gtest_main${LIBSUFFIX}")

file(MAKE_DIRECTORY ${GTEST_INCLUDES})

add_library(googleTest IMPORTED STATIC GLOBAL)
set_target_properties(googleTest PROPERTIES
  IMPORTED_LOCATION "${GTEST_LIBRARY}"
  INTERFACE_INCLUDE_DIRECTORIES "${GTEST_INCLUDES}"
  IMPORTED_LINK_INTERFACE_LIBRARIES "${CMAKE_THREAD_LIBS_INIT}")

add_library(googleTestMain IMPORTED STATIC GLOBAL)
set_target_properties(googleTestMain PROPERTIES
  IMPORTED_LOCATION "${GTEST_MAINLIB}"
  IMPORTED_LINK_INTERFACE_LIBRARIES "${GTEST_LIBRARY};${CMAKE_THREAD_LIBS_INIT}")

add_dependencies(googleTest googleTestExternal)
